/*
 * Copyright 2023 Grooviter
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'org.kordamp.gradle.java-project' version '0.47.0'
    id 'com.bmuschko.docker-remote-api'
}

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

config {
    release = (rootProject.findProperty('release') ?: false).toBoolean()  

    info {                                                                
        name          = 'Matter'
        vendor        = 'Grooviter'
        description   = 'Groovy Jupyter Kernel'
        inceptionYear = '2023'

        links {
            website      = 'https://github.com/grooviter/matter'
            issueTracker = 'https://github.com/grooviter/matter/issues'
            scm          = 'https://github.com/grooviter/matter.git'
        }

        people {
            person {
                id    = 'mariogarcia'
                name  = 'Mario Garcia'
                roles = ['developer']
            }
        }
    }

    licensing {                                                           
        licenses {
            license {
                id = 'Apache-2.0'
            }
            mergeStrategy = 'OVERRIDE'
        }
    }
}

allprojects {
    apply plugin: 'java'
    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
    config {
        docs {
            javadoc {
                enabled = false
            }
        }
    }
}

subprojects {
    java {
        sourceCompatibility = "1.8"
        targetCompatibility = "1.8"
    }
}

tasks.register("distributionCopyPython", Copy) {
    group("distribution")
    description("Copies Conda and Jupyter installation code to build dir")
    from fileTree('dist')
    into file("$buildDir/dist/")
}

tasks.register("distributionCopyJars", Copy) {
    group("distribution")
    description("Copies Groovy kernel jar libs to build dir")
    dependsOn(':matter-base:build', ':matter-groovy:build')
    from fileTree("$rootDir/modules/matter-groovy/build/dist/kernel")
    into file("$buildDir/dist/beakerx_groovy/kernel")
}

tasks.register("distributionDocker", DockerBuildImage) {
    group("distribution")
    description("Creates a jupyter image with Groovy kernel installed in it")
    dependsOn('distributionCopyPython', 'distributionCopyJars')
    inputDir = file("$buildDir/dist")
    images.add("${project.properties.group}/matter:$version")
}