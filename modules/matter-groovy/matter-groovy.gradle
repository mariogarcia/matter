/*
 *  Copyright 2020 TWO SIGMA OPEN SOURCE, LLC
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */
plugins {
    id 'application'
    id 'matter.copy-groovy-dist-files'
}

ext {
    antlr4Version = "4.5"
}

configurations {
    antlr
    runtimetools
}

dependencies {
    implementation project(":matter-base")
    implementation "org.codehaus.groovy:groovy:$groovy"
    implementation group: 'org.antlr', name: 'antlr4-runtime', version: "$antlr4Version"
    antlr group: 'org.antlr', name: 'antlr4', version: "$antlr4Version"
    runtimetools project(":matter-runtimetools")
    testImplementation project(":matter-base-test")
}

task antlr4_groovy_lexer(type: JavaExec) {
    main = "org.antlr.v4.Tool"
    workingDir = "src/main/antlr4/"
    classpath = configurations.antlr
    args = [
        "GroovyLexer.g4",
        "-o", "../java/com/twosigma/beakerx/groovy/autocomplete",
        "-listener",
        "-no-visitor",
        "-package", "com.twosigma.beakerx.groovy.autocomplete",
        "-encoding", "UTF-8"
    ]
}
task antlr4_groovy_parser(type: JavaExec) {
    main = "org.antlr.v4.Tool"
    workingDir = "src/main/antlr4/"
    classpath = configurations.antlr
    args = [
        "GroovyParser.g4",
        "-o", "../java/com/twosigma/beakerx/groovy/autocomplete",
        "-listener",
        "-no-visitor",
        "-package", "com.twosigma.beakerx.groovy.autocomplete",
        "-encoding", "UTF-8"
    ]
}

application {
    mainClassName = 'com.twosigma.beakerx.groovy.kernel.Groovy'
}

compileJava {
    dependsOn('antlr4_groovy_lexer', 'antlr4_groovy_parser')
}

jar {
    manifest {
        attributes "Main-Class": "$mainClassName"
    }
}