plugins {
    id 'application'
}

configurations {
    antlr
    runtimetools
}

dependencies {
    implementation project(":matter-base")
    implementation project(":matter-tablesaw")
    implementation "org.codehaus.groovy:groovy-all:$groovy"
    implementation "org.antlr:antlr4-runtime:$antlr4Version"
    antlr "org.antlr:antlr4:$antlr4Version"
    runtimetools project(":matter-runtimetools")
    testImplementation project(":matter-base-test")
}

application {
    mainClassName = 'com.twosigma.beakerx.groovy.kernel.Groovy'
}

compileJava {
    dependsOn('antlr4_groovy_lexer', 'antlr4_groovy_parser')
}

jar {
    manifest {
        attributes "Main-Class": "$mainClassName"
    }
}

task antlr4_groovy_lexer(type: JavaExec) {
    main = "org.antlr.v4.Tool"
    workingDir = "src/main/antlr4/"
    classpath = configurations.antlr
    args = [
        "GroovyLexer.g4",
        "-o", "../java/com/twosigma/beakerx/groovy/autocomplete",
        "-listener",
        "-no-visitor",
        "-package", "com.twosigma.beakerx.groovy.autocomplete",
        "-encoding", "UTF-8"
    ]
}
task antlr4_groovy_parser(type: JavaExec) {
    main = "org.antlr.v4.Tool"
    workingDir = "src/main/antlr4/"
    classpath = configurations.antlr
    args = [
        "GroovyParser.g4",
        "-o", "../java/com/twosigma/beakerx/groovy/autocomplete",
        "-listener",
        "-no-visitor",
        "-package", "com.twosigma.beakerx.groovy.autocomplete",
        "-encoding", "UTF-8"
    ]
}

tasks.register("copyDistDependencies", Copy) {
    from configurations.runtimeClasspath
    from jar
    into new File("$buildDir/dist/kernel/", 'lib')
}

tasks.register("copyDistRuntimetools", Copy) {
    from configurations.runtimetools
    from jar
    into new File("$buildDir/dist/kernel/", 'ext')
    include 'matter-runtimetools*.jar'
}

distZip.dependsOn installDist

jar.finalizedBy copyDistDependencies
jar.finalizedBy copyDistRuntimetools