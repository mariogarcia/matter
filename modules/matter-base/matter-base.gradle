/*
 *  Copyright 2020 TWO SIGMA OPEN SOURCE, LLC
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */
plugins {
    id 'java-library'
}

configurations {
    provided
    customCompilation.extendsFrom(compilation, api, implementation)
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
    test {
        compileClasspath += configurations.provided
        runtimeClasspath += configurations.provided
    }
}

ext {
    resourceDir = file("$projectDir/src/main/resources")
}

dependencies {
    api project(':matter-base-api')
    provided project(':matter-doclet')
    api group: 'com.opencsv', name: 'opencsv', version: '3.10'
    api group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.6.5'
    api group: 'com.github.jupyter', name: 'jvm-repr', version: '0.3.1'
    api group: 'commons-io', name: 'commons-io', version: '2.5'
    api group: 'commons-cli', name: 'commons-cli', version: '1.2'
    api group: 'org.apache.maven.shared', name: 'maven-invoker', version: '3.0.0'
    api group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.25'

    api group: "org.codehaus.groovy", name: "groovy", version: "$groovy"

    api group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.5'
    api 'io.javalin:javalin:1.7.0'
    api group: "org.zeromq", name: "jeromq", version: "0.3.5"
    api group: 'org.apache.ivy', name: 'ivy', version: '2.4.0'

    implementation group: 'junit', name: 'junit', version: '4.11'
    implementation group: 'org.assertj', name: 'assertj-core', version: '3.6.1'

    testImplementation group: 'org.reflections', name: 'reflections', version: '0.9.10'
    testImplementation project(':matter-base-test')
}

static String getGitHash() {
    return "git log --pretty=format:%h -n 1".execute().text.trim()
}

static String getGitCommitHash() {
    return "git rev-list --tags --max-count=1".execute().text.trim()
}

static String getGitLatestTagVersion() {
    return "git describe --tags ${getGitCommitHash()}".execute().text.trim()
}

task prepareBuildVersion() {
    doLast {
        file("$resourceDir/version").write(getGitLatestTagVersion())
        file("$resourceDir/hash").write(getGitHash())
        file("$resourceDir/build_time").write(new Date().format("yyyy-MM-dd HH:mm z"))
        file("$rootDir/VERSION").write(getGitLatestTagVersion())
    }
}

task jvmInspectJavaDoc(type: Javadoc) {
    source = sourceSets.main.allJava
    classpath = configurations.provided + configurations.customCompilation
    options.doclet = "com.twosigma.beakerx.doclet.BeakerxDoclet"
    options.docletpath = [file("../matter-doclet/build/libs/matter-doclet-${version}.jar")]
    options.addStringOption("subpackages", "com.twosigma.beakerx")
    options.addStringOption("sourcepath", "$projectDir/src/main/java")
    options.addStringOption("encoding", "UTF-8")
}

task moveInspectJson() {
    doFirst {
        copySpec {
            from("$projectDir/matter_inspect.json")
            into("$buildDir/resources/main")
        }
    }
    doLast {
        file("$projectDir/matter_inspect.json").delete()
    }
}

compileJava.dependsOn prepareBuildVersion
compileJava.finalizedBy jvmInspectJavaDoc
jar.dependsOn moveInspectJson